package com.sample
 
import java.util.Date;

import klient.Rezerwacja
import klient.Zwrot

import com.auto.Samochod
 
/**
 * Regula sprawdza czy w danym terminie dla danej rezerwacji istnieje samochod w danej klasie
 * Jezeli nie to zwiekszana jest klasa samochodu bez zwiekszania jego ceny
*/
rule "Zwieksz klase samochodu"
	when
		r : Rezerwacja( przyjeta == false && zakonczona == false && samochod.klasa < 2, myKlasa : samochod.klasa)
		forall( s: Samochod(klasa == myKlasa)
				Rezerwacja(samochod == s && przyjeta == true && (dateBetween(r.dataOd) == true || dateBetween(r.dataDo) == true)) )
	then
		r.samochod.klasa += 1;
		System.out.println("Zwiekszenie klasy");
		update(r);
end

/**
	* Regula sprawdza czy istnieje taka rezerwacja która nie zostala przyjeta oraz nie jest zakonczona.
	* Nastepnie sprawdzamy czy istnieje samochód danej klasy w danym terminie.
	* Oraz sprawdzamy czy klient nie ma innych przyjetych ale nie zakonczonych rezerwacji.
*/
rule "Rezerwuj Samochod"
	when
		r : Rezerwacja( przyjeta == false && zakonczona == false, myKlasa : samochod.klasa)
		s : Samochod(klasa == myKlasa)
	not Rezerwacja(samochod == s && przyjeta == true && (dateBetween(r.dataOd) == true || dateBetween(r.dataDo) == true)) 
	not Rezerwacja(klient == r.klient && przyjeta == true && zakonczona == false)
	then
	r.przyjeta = true;
	r.cenaOd3 = r.samochod.cenaOd3;
	r.cenaDo3 = r.samochod.cenaDo3;
	r.samochod = s;
	update(r);
end

/**
	* Regula sprawdza czy istnieje jakis zwrot który nie zostal jeszcze podsumowany(zwrócony).
*/
rule "Zwrot Samochodu"
	when
		z : Zwrot(zwrocony == false)
	then
		double cena = z.rezerwacja.samochod.cenaZaSamochod(z);
		System.out.println(z.rezerwacja.klient.name+ " " + z.rezerwacja.samochod.getClass().getSimpleName() +" "+cena);
		z.zwrocony = true;
		z.rezerwacja.zakonczona = true;
		update(z);
end

/**
	* Regula sprawdza czy istnieje klient który w danym okresie posiada rezerwacjé która zostala juz przyjeta.
	* Rezerwacja ta zostaje oznaczona jako zakonczona, ale nie przyjeta.
*/
rule "Usuwanie zlych rezerwacji"
	when
	r : Rezerwacja( przyjeta == false && zakonczona == false, myKlasa : samochod.klasa)
	Rezerwacja(przyjeta == true && klient == r.klient && (dateBetween(r.dataOd) == true || dateBetween(r.dataDo) == true)) 
	then
	r.zakonczona = true;
	System.out.println("Istnieje rezerwacja w tym terminie");
	update(r);
end